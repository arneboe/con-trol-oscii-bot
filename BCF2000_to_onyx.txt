@input osc_in OSC "192.168.44.44:9000"
@output osc_out OSC "192.168.44.44:8000" 1024 0
@input midi_in MIDI "BCF2000"
@output midi_out MIDI "BCF2000"

@init
fader_midi_control_numbers = 1024;
downdown_button_midi_control_numbers = 4000;
down_button_midi_control_numbers = 6000;
effect_belt_midi_control_numbers =8000;

/////////////////////////////
//////BEGIN CONFIGURATION////
/////////////////////////////

//set midi control numbers of the faders
//Faders need to be in CC Mode and send values in the range [0..127]
//Example: If Fader 1 sends CC messages using control number 42, you would set the following:
//         fader_midi_control_numbers[0] = 42;
//         Note that the array index is zero-based. Our faders have numbers [1..8] while our array indices have numbers [0..7].
fader_midi_control_numbers[0] = 1;
fader_midi_control_numbers[1] = 2;
fader_midi_control_numbers[2] = 3;
fader_midi_control_numbers[3] = 4;
fader_midi_control_numbers[4] = 5;
fader_midi_control_numbers[5] = 6;
fader_midi_control_numbers[6] = 7;
fader_midi_control_numbers[7] = 8;

downdown_button_midi_control_numbers[0] = 9;
downdown_button_midi_control_numbers[1] = 10;
downdown_button_midi_control_numbers[2] = 11;
downdown_button_midi_control_numbers[3] = 12;
downdown_button_midi_control_numbers[4] = 13;
downdown_button_midi_control_numbers[5] = 14;
downdown_button_midi_control_numbers[6] = 15;
downdown_button_midi_control_numbers[7] = 16;

down_button_midi_control_numbers[0] = 17;
down_button_midi_control_numbers[1] = 18;
down_button_midi_control_numbers[2] = 19;
down_button_midi_control_numbers[3] = 20;
down_button_midi_control_numbers[4] = 21;
down_button_midi_control_numbers[5] = 22;
down_button_midi_control_numbers[6] = 23;
down_button_midi_control_numbers[7] = 24;

next_button_midi_control_number = 25;
prev_button_midi_control_number = 26;

effect_belt_midi_control_numbers[0] = 27;
effect_belt_midi_control_numbers[1] = 28;
effect_belt_midi_control_numbers[2] = 29;
effect_belt_midi_control_numbers[3] = 30;


/////////////////////////////
//////END CONFIGURATION//////
/////////////////////////////

midi_channel_to_fader_num = 2000;
midi_channel_to_downdown_button_num = 5000;
midi_channel_to_down_button_num = 7000;
midi_channel_to_effect_belt_num = 9000;

while(
	midi_channel_to_effect_belt_num[effect_belt_midi_control_numbers[i]] = i;
	i = i + 1;
	i < 4;
   );


i = 0;
while(
	midi_channel_to_fader_num[fader_midi_control_numbers[i]] = i;
	midi_channel_to_downdown_button_num[downdown_button_midi_control_numbers[i]] = i;
	midi_channel_to_down_button_num[down_button_midi_control_numbers[i]] = i;
	i = i + 1;
	i < 8;
   );


//mapf from arduino (https://gist.github.com/nadavmatalon/71ccaf154bc4bd71f811289e78c65918#file-mapf-ino-L2)
 function mapf(val in_min in_max out_min out_max)
 (
 	(val - in_min) * (out_max - out_min) / (in_max - in_min) + out_min;
 );


@oscmsg

match("/Mx/fader/%i", oscstr) ?
(
	fader_num = (fmt0 - 4203) / 10;
	midi_chan = fader_midi_control_numbers[fader_num];
	//176, 2, 0
	msg1=176;
	msg2=midi_chan;
	msg3=mapf(oscparm(0), 0, 255, 0, 127);
	midisend(midi_out);
)

@midimsg
//printf("%d, %d, %d\n", msg1, msg2, msg3);


//Faders
msg1 == 0xB0? (
	osc_fader_number = midi_channel_to_fader_num[msg2] * 10 + 4203;
	fader_value = mapf(msg3, 0, 127, 0, 255);
	oscsend(osc_out, "i/Mx/fader/%d", fader_value, osc_fader_number);
);

//Buttons
//down down buttons (on ch 2)
msg1 == 0xB1? (
	osc_button_number = midi_channel_to_downdown_button_num[msg2] * 10 + 4205;
	msg3 == 127 ? (
		oscsend(osc_out, "i/Mx/button/%d", 1, osc_button_number);
	) : (
		oscsend(osc_out, "i/Mx/button/%d", 0, osc_button_number);
	)
);

//down buttons (on ch 3)
msg1 == 0xB2? (
	osc_button_number = midi_channel_to_down_button_num[msg2] * 10 + 4204;
	msg3 == 127 ? (
		oscsend(osc_out, "i/Mx/button/%d", 1, osc_button_number);
	) : (
		oscsend(osc_out, "i/Mx/button/%d", 0, osc_button_number);
	)
);

//bank select (on ch 4)
msg1 == 0xB3? (
	msg2 == prev_button_midi_control_number ? (
	msg3 == 127 ? (
			oscsend(osc_out, "i/Mx/scroll/4110/down", 1);
		) : (
			oscsend(osc_out, "i/Mx/scroll/4110/down", 0);
		)
	);

	msg2 == next_button_midi_control_number ? (
	msg3 == 127 ? (
			oscsend(osc_out, "i/Mx/scroll/4110/up", 1);
		) : (
			oscsend(osc_out, "i/Mx/scroll/4110/up", 0);
		)
	);
);

//Faders
msg1 == 0xB4? (
	osc_fader_number = midi_channel_to_effect_belt_num[msg2] * 10 + 6212;
	val = 0;
	msg3 > 0 ? (
		val = 1;
	);
	oscsend(osc_out, "i/Mx/belt/%d", val, osc_fader_number);
);
